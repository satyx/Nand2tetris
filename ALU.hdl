// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

    Not(in=zx,out=notzx);
    Not(in=zy,out=notzy);

    Mux16(a=x,b=false,sel=zx,out=x1);
    Not16(in[0..15]=x1,out[0..15]=notx1);

    Mux16(a[0..15]=x1,b[0..15]=notx1,sel=nx,out[0..15]=x2);


    Mux16(a=y,b=false,sel=zy,out=y1);
    Not16(in[0..15]=y1,out[0..15]=noty1);

    Mux16(a[0..15]=y1,b[0..15]=noty1,sel=ny,out[0..15]=y2);

    Not(in=f,out=notf);
    Add16(a[0..15]=x2,b[0..15]=y2,out[0..15]=out1);
    And16(a[0..15]=x2,b[0..15]=y2,out[0..15]=out2);
    Mux16(a[0..15]=out1,b[0..15]=out2,sel=notf,out[0..15]=outf);

    Not16(in[0..15]=outf,out[0..15]=notoutf);
    Mux16(a[0..15]=outf,b[0..15]=notoutf,sel=no,out[0..15]=out,out[15]=ng,out[0..7]=zr1,out[8..15]=zr2);

    Or8Way(in[0..7]=zr1,out=or1);
    Or8Way(in[0..7]=zr2,out=or2);
    Or(a=or1,b=or2,out=or3);
    Not(in=or3,out=zr);
}